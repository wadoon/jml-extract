allprojects {
    group = "jml.extractor"
    version = "0.0.1"

    repositories {
        mavenCentral()
    }
}


subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    task sourcesJar(type: Jar) {
        classifier = "sources"
        from(sourceSets.main.allSource)
    }

    dependencies {
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.2")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.2")
        compileOnly("org.jetbrains:annotations:16.0.2")
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/wadoon/jml-extract")
                credentials {
                    username = project.findProperty("gpr.user") as String ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") as String ?: System.getenv("PASSWORD")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from components["java"]
                artifact sourcesJar
            }
        }
    }
}
